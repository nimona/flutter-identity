def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

group 'io.nimona.gohashmobile'
version '1.0-SNAPSHOT'

buildscript {
    ext.kotlin_version = '1.1.51'
    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

rootProject.allprojects {
    repositories {
        google()
        jcenter()

        // FIXME workaround issue https://github.com/golang/go/issues/25184
        // this is used to include the mobileapi AAR as a dependency.
        // When this issue is fixed, remove this and the dependency declaration on the AAR.
        // For development, comment out the "include 'mobileapi' line in settings.gradle, then
        // change the commented-out line in the dependencies section to use the AAR, not the project.
        flatDir {
            dirs 'mobileapi'
        }
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 27

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    defaultConfig {
        minSdkVersion 16
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    compileOnly files("$flutterRoot/bin/cache/artifacts/engine/android-arm/flutter.jar")

    // FIXME uncomment this line for development so the Mobileapi classes are visible from Java.
    // For the test app to work, the dependency on the project must be used instead of on the AAR.
    //implementation name: 'mobileapi', ext: 'aar'
    implementation project(':mobileapi')
}
